import java.sql.Timestamp

version '0.7.0'

apply plugin: 'java'

sourceCompatibility = 1.8

jar {
    manifest {
        attributes 'Implementation-Title': 'ProMaSy',
                'Implementation-Version': version,
                'Main-Class': 'App'
    }

    doFirst {
        from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
            exclude 'META-INF/MANIFEST.MF'
            exclude 'META-INF/*.SF'
            exclude 'META-INF/*.DSA'
            exclude 'META-INF/*.RSA'
        }
    }

    doLast {
        copy {
            from "reports/Bids_Report.jrxml"
            into "build/libs/reports"
        }
    }
}

task createVersionProperties(dependsOn: processResources) {
    doFirst {
        new File("src/main/resources/version.properties").withWriter { w ->
            Properties p = new Properties()
            p.setProperty('versionNumber', project.version.toString())
            p.setProperty('buildDate', new Timestamp(System.currentTimeMillis()).toString())
            p.store w, null
        }
        copy {
            from "src/main/resources/version.properties"
            into "$buildDir/resources/main"
        }
    }
}

classes {
    dependsOn createVersionProperties
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://repo.spring.io/release" }
    maven { url "http://jaspersoft.artifactoryonline.com/jaspersoft/third-party-ce-artifacts/" }
}

dependencies {
    // https://mvnrepository.com/artifact/org.postgresql/postgresql
    compile group: 'org.postgresql', name: 'postgresql', version: '9.4.1212'

    // https://mvnrepository.com/artifact/net.sf.jasperreports/jasperreports
    compile group: 'net.sf.jasperreports', name: 'jasperreports', version: '6.4.0'

    // https://mvnrepository.com/artifact/org.swinglabs.swingx/swingx-all
    compile group: 'org.swinglabs.swingx', name: 'swingx-all', version: '1.6.5-1'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'

    // https://mvnrepository.com/artifact/org.codehaus.plexus/plexus-utils
    compile group: 'org.codehaus.plexus', name: 'plexus-utils', version: '3.0.24'

    // https://mvnrepository.com/artifact/com.github.jiconfont/jiconfont-swing
    compile group: 'com.github.jiconfont', name: 'jiconfont-swing', version: '1.0.1'

    // https://mvnrepository.com/artifact/com.github.jiconfont/jiconfont-font_awesome
    compile group: 'com.github.jiconfont', name: 'jiconfont-font_awesome', version: '4.5.0.3'

    // https://mvnrepository.com/artifact/org.hibernate/hibernate-core
    compile group: 'org.hibernate', name: 'hibernate-core', version: '5.2.6.Final'

    // https://mvnrepository.com/artifact/org.hibernate/hibernate-jpamodelgen
    compile 'org.hibernate:hibernate-jpamodelgen:5.2.6.Final'

    // https://mvnrepository.com/artifact/log4j/log4j
    compile group: 'log4j', name: 'log4j', version: '1.2.17'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}